plugins {
    id 'java'
    id 'jvm-test-suite'
    id "com.diffplug.spotless" version "7.2.1" // 8.1.0 conflicts with jreleaser
    id "com.github.spotbugs" version "6.4.8"
    id 'maven-publish'
    id 'org.jreleaser' version '1.22.0'
}

group = 'com.github.marianobarrios'
version = System.getenv('VERSION')

var javaToolchain = System.getenv('JAVA_TOOLCHAIN')

java {
    // If running from CI, the tool chain will be present
    if (javaToolchain != null) {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaToolchain)
        }
    }
}

compileJava {
    options.release = 8
    // for some reason javac warns the previous option, disabling
    options.compilerArgs.add('-Xlint:all,-options')
}

compileTestJava {
    options.release = 8
    // for some reason javac warns the previous option, disabling
    options.compilerArgs.add('-Xlint:all,-options,-try')
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // log
    testImplementation 'org.slf4j:jul-to-slf4j:2.0.17'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.5.24'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.14.2'
}

spotbugs {
    ignoreFailures = false
    showProgress = true
    reportsDir = file("$buildDir/spotbugs")
}

spotbugsMain {
    excludeFilter = file('spotbugs-exclude.xml')
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/spotbugs.html")
        }
    }
}

spotbugsTest {
    excludeFilter = file('spotbugs-exclude-tests.xml')
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/spotbugsTest.html")
        }
    }
}

var testJavaToolchain = System.getenv('TEST_JAVA_TOOLCHAIN')

testing {
    suites {
        test {
            useJUnitJupiter()
            targets {
                configureEach {
                    testTask.configure {
                        if (testJavaToolchain != null) {
                            javaLauncher.set(
                                    javaToolchains.launcherFor {
                                        languageVersion.set(JavaLanguageVersion.of(testJavaToolchain))
                                    }
                            )
                        }
                        // override security properties enabling all options
                        systemProperty "java.security.properties", "java.security.override"
                        systemProperty "junit.jupiter.extensions.autodetection.enabled", "true"
                    }
                }
            }
        }
    }
}

javadoc {
    exclude "tlschannel/impl"
    exclude "tlschannel/util"
}

java {
    withSourcesJar()
    withJavadocJar()
}

// see tlschannel.AllocationTest for comment about this
tasks.register('allocationTest', JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'tlschannel.AllocationTest'
    jvmArgs = ['-XX:+UnlockExperimentalVMOptions', '-XX:+UseEpsilonGC']
}

check.dependsOn allocationTest

publishing {
    publications {
        tlschannel(MavenPublication) {
            artifactId = 'tls-channel'
            from components.java
            pom {
                name = 'TLS Channel'
                description = 'A Java library that implements a ByteChannel interface over SSLEngine, ' +
                        'enabling easy-to-use (socket-like) TLS for Java applications. '
                url = 'https://github.com/marianobarrios/tls-channel'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        name = 'Mariano Barrios'
                        email = 'marbar@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:marianobarrios/tlschannel.git'
                    developerConnection = 'scm:git@github.com:marianobarrios/tlschannel.git'
                    url = 'https://github.com/marianobarrios/tls-channel'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

